import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def timestamp(boolean time) {
    def dateFormat = new SimpleDateFormat(time ? "HHmm" : "yyyy-MM-dd")
    dateFormat.setTimeZone(TimeZone.getTimeZone("UTC"))
    return dateFormat.format(new Date())
}

def idForSimon() {
    try {
        def commit = new ByteArrayOutputStream()
        exec {
            // Require remote called simon because someone downstream might call my branch "upstream"
            commandLine 'git', 'merge-base', 'simon/main', 'main'
            standardOutput = commit
        }
        def shortUnique = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', commit.toString().trim()
            standardOutput = shortUnique
        }
        return shortUnique.toString().trim()
    } catch (ignored) {
        return "UNOFFICIAL"
    }
}

def issuesURL() {
    def gitRemote = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'ls-remote', '--get-url', 'origin'
        standardOutput = gitRemote
    }
    return gitRemote.toString().trim().replaceFirst("\\.git\$", "") + "/issues"
}

android {
    namespace 'name.boyle.chris.sgtpuzzles'
    compileSdkVersion 33
    defaultConfig {
        targetSdkVersion 33
        minSdkVersion 19
        applicationId "name.boyle.chris.sgtpuzzles"
        versionCode 133
        versionName timestamp(false)
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        resValue "string", "issues_url", issuesURL()
    }
    sourceSets.androidTest.assets.srcDirs += new File(project.buildDir, "testGames")
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            versionNameSuffix "-DEBUG-${idForSimon()}"
            ndk {
                debuggable true
                debugSymbolLevel "FULL"
            }
        }
        release {
            versionNameSuffix "-${timestamp(true)}-${idForSimon()}"
            minifyEnabled true
            ndk {
                debuggable false
                debugSymbolLevel "FULL"
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

task copyTestGames(type: Copy) {
    from('src/main/jni/icons') {
        include '*.sav'
    }
    into new File(project.buildDir, 'testGames')
}

// Generate a Java enum of games: really this should use some CMake output instead of the regex
// hack, but the Android Gradle Plugin wants to compile the Java before running any of the CMake
project.extensions.findByName("android").applicationVariants.all { variant ->
    tasks["merge${variant.name.capitalize()}Assets"].dependsOn(copyTestGames)
    def sourceDir = new File(project.getBuildDir(), "generated/backendsEnum/${variant.name}")
    def myTask = tasks.register("generate${variant.name.capitalize()}BackendsEnum") {
        it.inputs.dir('src/main/jni')
        it.outputs.dir(sourceDir)
        it.doLast {
            def backends = new File(projectDir, 'src/main/jni/CMakeLists.txt').text.findAll(~/puzzle\(\s*(\w+)\s+DISPLAYNAME\s+"([^"]+)"/) { m, puz, display ->
                def text = new File(projectDir, "src/main/jni/${puz}.c").text
                def match = text =~ /enum\s+\{\s*COL_[^,]+,\s*(COL_[^}]+)}/
                def colours = []
                if (match) {
                    colours = match.group(1).replaceAll(/(?s)\/\*.*?\*\//, "").replaceAll(/#[^\n]*\n/, "")
                            .trim().split(",")*.trim()*.replaceFirst(/^COL_/, "")*.toLowerCase(Locale.ROOT)
                            .findAll { it =~ /^[^=]+$/ } - ["ncolours", "crossedline"]
                    if (colours.any { it =~ /[^a-z\d_]/ }) {
                        throw new Exception("Couldn't parse colours for " + puz + ": " + match.group(1) + " -> " + colours)
                    }
                }
                [puz, display, ('new String[]{"' + colours.join('", "') + '"}')] }
            def out = new File("${it.outputs.files.singleFile}/name/boyle/chris/sgtpuzzles/BackendName.java")
            out.parentFile.mkdirs()
            out.delete()
            out.text = """package name.boyle.chris.sgtpuzzles;
                    |import java.util.Collections;
                    |import java.util.LinkedHashMap;
                    |import java.util.Locale;
                    |import java.util.Map;
                    |import java.util.Objects;
                    |import android.content.Context;
                    |import android.graphics.drawable.Drawable;
                    |import androidx.annotation.DrawableRes;
                    |import androidx.annotation.StringRes;
                    |import androidx.annotation.NonNull;
                    |import androidx.annotation.Nullable;
                    |import androidx.core.content.ContextCompat;
                    |
                    |/** Names of all the backends. Automatically generated file, do not modify. */
                    |public enum BackendName {
                    |    ${backends.collect {"${it[0].toUpperCase(Locale.ROOT)}(\"${it[1]}\", R.drawable.${it[0]}, R.string.desc_${it[0]}, ${it[2]})"}.join(",\n    ")};
                    |    private final String _displayName;
                    |    private final String[] _colours;
                    |    @DrawableRes private final int _icon;
                    |    @StringRes private final int _description;
                    |    private BackendName(@NonNull final String displayName, @DrawableRes final int icon, @StringRes final int description, @NonNull final String[] colours) { _displayName = displayName; _icon = icon; _description = description; _colours = colours; }
                    |    @NonNull public String getDisplayName() { return _displayName; }
                    |    @NonNull public Drawable getIcon(final Context context) { return Objects.requireNonNull(ContextCompat.getDrawable(context, _icon)); }
                    |    @StringRes public int getDescription() { return _description; }
                    |    @NonNull public String[] getColours() { return _colours; }
                    |    @NonNull public String toString() { return name().toLowerCase(Locale.ROOT); }
                    |    public boolean isLatin() { return this == KEEN || this == SOLO || this == TOWERS || this == UNEQUAL; }
                    |    private static final Map<String, BackendName> BY_DISPLAY_NAME, BY_LOWERCASE;
                    |    static {
                    |        final Map<String, BackendName> byDisp = new LinkedHashMap<String, BackendName>(), byLower = new LinkedHashMap<String, BackendName>();
                    |        for (final BackendName bn : values()) { byDisp.put(bn.getDisplayName(), bn); byLower.put(bn.toString(), bn); }  // no streams until API 24
                    |        BY_DISPLAY_NAME = Collections.unmodifiableMap(byDisp);
                    |        BY_LOWERCASE = Collections.unmodifiableMap(byLower);
                    |    }
                    |    @UsedByJNI @Nullable public static BackendName byDisplayName(final String displayName) {
                    |        return BY_DISPLAY_NAME.get(displayName);
                    |    }
                    |    @UsedByJNI @Nullable public static BackendName byLowerCase(final String lowerCase) {
                    |        return BY_LOWERCASE.get(lowerCase);
                    |    }
                    |};
                    |""".stripMargin()
        }
    }
    variant.registerJavaGeneratingTask(myTask, sourceDir)
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.webkit:webkit:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.5.1'
    androidTestImplementation 'androidx.annotation:annotation:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
}
